<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;

    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    void main(void)
    {
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        // normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);

        //rim Lighting
        float NdotP = dot(normal, v_Position) + 1.2;
        vec3 rimColor = vec3(0,0,0);
        if (NdotP > 0.01)  
            rimColor = vec3(NdotP/4.0, NdotP/2.0, NdotP/2.0);

        // bump mapping
        float center = texture2D(u_Bump,v_Texcoord).x;  
        float right = texture2D(u_Bump,v_Texcoord + vec2(1.0/1024.0,0)).x;
        float top = texture2D(u_Bump,v_Texcoord + vec2(0,1.0/512.0)).x;
        vec3 bNormal = normalize(vec3(center-right, center-top, 0.2));
        vec3 pToEye = eastNorthUpToEyeCoordinates(v_positionMC, normal) * bNormal;
        normal = normalize(pToEye);
        
        float diffuse = clamp(dot(u_CameraSpaceDirLight, normal), 0.0, 1.0);
        float diffuse_noclamp = dot(u_CameraSpaceDirLight, normal); // use it to detect if it is night or daytime

        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);

        float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2

        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
        vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;        
        vec3 specColor = texture2D(u_EarthSpec, v_Texcoord).rgb; // black:land, white:sea
        
        // cloud animation
        vec2 v_moveCoord = v_Texcoord - vec2(u_time, 0.0);
       
        vec3 cloudColor = texture2D(u_Cloud, v_moveCoord).rgb; 
        vec3 cloudTransColor = texture2D(u_CloudTrans, v_moveCoord).rgb;

        if(specColor.x > 0.0) //white, is sea
        {
            specColor = specColor * specular;
        }else // is land
        {
            specColor = vec3(0,0,0);
        }
        //apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));
        nightColor = mix(nightColor, (vec3(1,1,1) - cloudTransColor), 0.4);

        vec3 color = vec3(0,0,0);
        vec3 colortmp = ((0.6 * diffuse) + (0.4 * specColor)) * dayColor;
        colortmp = mix(colortmp, cloudColor * cloudTransColor, 0.3);
        color = mix(nightColor, colortmp, (1.0 + diffuse_noclamp)/2.0);

        color += rimColor;
        gl_FragColor = vec4(color, 1.0);
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="js/lib/gl-matrix.js" type ="text/javascript"></script>
<script src ="js/webGLUtility.js" type ="text/javascript"></script>
<script src ="js/frag_globe.js" type ="text/javascript"></script>
</body>

</html>
